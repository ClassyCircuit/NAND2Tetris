// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // makes x zero
    Not16(in=x, out=notX);
    And16(a=x, b=notX, out=zeroX);
    // 'zx' checkpoint (x or zeroed-x)
    Mux16(a=x, b=zeroX, sel=zx, out=XAfterZX);
    // negates x
    Not16(in=XAfterZX, out=notXAfterZX);
    // 'nx' checkpoint (zx output or negated zx output)
    Mux16(a=XAfterZX, b=notXAfterZX, sel=nx, out=XAfterNX);

    // makes y zero
    Not16(in=y, out=notY);
    And16(a=y, b=notY, out=zeroY);
    // 'zy' checkpoint (y or zeroed-y)
    Mux16(a=y, b=zeroY, sel=zy, out=YAfterZY);
    // negates y
    Not16(in=YAfterZY, out=notYAfterZY);
    // 'ny' checkpoint (zy output or negated zy output)
    Mux16(a=YAfterZY, b=notYAfterZY, sel=ny, out=YAfterNY);

    // calculate function outcomes
    And16(a=XAfterNX, b=YAfterNY, out=xANDy);
    Add16(a=XAfterNX, b=YAfterNY, out=xPLUSy);
    // 'f' checkpoint (x+y or x&y)
    Mux16(a=xANDy, b=xPLUSy, sel=f, out=XYAfterF);
    // negates function outcome
    Not16(in=XYAfterF, out=notXYAfterF);
    // 'no' checkpoint (f or negated f)
    Mux16(a=XYAfterF, b=notXYAfterF, sel=no, out=out, out[0..7]=outputHalf1, out[8..15]=outputHalf2, out[15]=ngControl);

    // 'zr' output logic
    Or8Way(in=outputHalf1, out=zrInput1);
    Or8Way(in=outputHalf2, out=zrInput2);
    Or(a=zrInput1, b=zrInput2, out=isOutputPositive);
    Not(in=isOutputPositive, out=zr);

    // 'ng' output logic
    And(a=ngControl, b=ngControl, out=ng);


}